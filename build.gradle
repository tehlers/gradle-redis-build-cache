plugins {
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.10.1'
}

group = 'net.idlestate'
version = '1.3.0'

repositories {
    jcenter()
}

dependencies {
    implementation 'redis.clients:jedis:3.2.0'
}

gradlePlugin {
    plugins {
        redisBuildCache {
            id = 'net.idlestate.gradle-redis-build-cache'
            implementationClass = 'net.idlestate.gradle.caching.RedisBuildCachePlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/tehlers/gradle-redis-build-cache'
    vcsUrl = 'https://github.com/tehlers/gradle-redis-build-cache.git'

    plugins {
        redisBuildCache {
            id = 'net.idlestate.gradle-redis-build-cache'
            displayName = 'Redis Build Cache'
            description = "A Gradle build cache implementation that uses Redis to store the build artifacts. Since this is a settings plugin the build script snippets below won't work. Please consult the documentation at Github."
            tags = ['build-cache', 'redis', 'cache']
        }
    }
}

task sourceJar( type: Jar ) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        gradlePlugin( MavenPublication ) {
            artifactId archivesBaseName
            from components.java

            artifact sourceJar {
                archiveClassifier = 'sources'
            }

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    description 'A Gradle plugin that uses Redis as a build cache'
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }
}
